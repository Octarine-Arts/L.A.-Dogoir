title: JohnnyMaloneDialogue
tags:NPC, Detective
position: -338,-113
---
<<if $JohnnyIsInitialGreeting == false>>
    Johnny: You buttons ain’t behind the eight-ball, are you?
    Detective: Excuse me?
    Johnny: Here before the body’s even cold. Got peepers on the bar or something?
    Detective: Why, anything we need to be peeping on? 
    <<set $JohnnyIsInitialGreeting to true>>
<<elseif $JohnnyIsInitialGreeting == true>>
    <<set $JohnnyRandomGreeting to random_range(0,2)>>
    <<if $JohnnyRandomGreeting == 0>>
        Johnny: Mr. Gumshoe returns.
    <<elseif $JohnnyRandomGreeting == 1>>
        Johnny: Forget something?
    <<else>>
        Johnny: What now, buttons?
    <<endif>>
<<endif>>

<<jump JohnnyQuestions>>

===
title: JohnnyQuestions
position: 69,-84
---
-> Detective: Who are you?
    <<jump JohnnyWhoAreYou>>

-> Detective: Gunshot?
    <<jump JohnnyGunshot>>

-> Detective: Mr.Harlow?
    <<jump JohnnyDoYouKnowJack>>

-> Detective: Seen anything suspiscious? <<if $JohnnyKnowsSomething == true>>
    <<jump JohnnyIsSuspiscious>>

-> Detective:  Dale? <<if $Dale == true>> 
    <<jump JohnnyKnowsDale>>

-> Detective:  Loretta? <<if $Loretta == true>> 
    <<jump JohnnyKnowsLoretta>>

-> Detective:  Peggy? <<if $Peggy == true>> 
    <<jump JohnnyKnowsPeggy>>

-> Detective:  I found these horse tickets <<if $DetectiveHasFoundHorseTicket == true>> 
    <<jump AboutHorseTickets>>

-> Detective:  What where you doing in the ladies? <<if $DetectiveHasSpokenToLorettaBlood == true && $DetectiveHasFoundBlood == true && $DetectiveHasSpokenToJohnnyHorseTicket == true>>
    <<jump AboutTheBloodInTheLadies>>

-> Detective: You know Jack brings his own flask to a bar? <<if $DetectiveHasSpokenToPeggyFlask == true && $DetectiveHasFoundFlask == true>>
    <<jump AboutTheFlaskAndDale>>

-> Detective: All the evidence is pointing at you <<if  $DetectiveHasSpokenToJohnnyDebt == true && $DetectiveHasSpokenToJohnnyHorseTicket == true && $DetectiveHasSpokenToJohnnyBlood == true>>
    <<jump JohnnyInterrogate>>

-> Detective: Goodbye
    <<jump JohnnyGoodbye>>

===
title: JohnnyWhoAreYou
position: 138,-404
---
Detective: Who are you, exactly?
Johnny: Johnny Malone. People call me Johnny “Two Sleeves”.

<<jump JohnnyQuestions>>
===

title: JohnnyGunshot
position: -111,-477
---
Detective: Where were you when the murder occurred?
Johnny: I was in the men’s room. I assume you don’t need more detail than that? Because it was quite hard to aim when them lights went out, if you get my meaning. Then the gunshot happened, lights came back on, I came out to one dead Harlow.

<<set $JohnnyKnowsSomething to true>>
<<jump JohnnyQuestions>>

===
title: JohnnyIsSuspiscious
position: 449,-578
---
Detective: You notice anything suspicious when you came out?
Johnny: Yeah, his wife. She came running in from out the back. She was still in the bar when I left. Don’t know why a dame like her’d wanna go out into the back alley.

<<set $Johnny to true>>
<<set $JohnnySawPeggyFromAlley to true>>
<<jump JohnnyQuestions>>
===

title: JohnnyDoYouKnowJack
position: 459,-353
---
Detective: You don’t seem too beat up about Mr. Harlow’s murder.
Johnny: Jack Harlow got what was coming to him. Guy couldn’t buy dog soup, but he’s in here begging Dale for booze and chatting up broads most nights. Thinks he’s a hotshot, but he ain’t squat.

<<set $Johnny to true>>
<<jump JohnnyHowDidYouKnowHim>>
===

title: JohnnyHowDidYouKnowHim
position: 459,-353
---
Detective: You have a strong opinion of Jack Harlow, how did you know him so well?
Johnny: Got myself a little share in the bar - all legal, so don’t blow your wig - so yeah, I see him in here, scaring the rest of the clientele back through the doors. That’s all I need to know about him.

<<set $Johnny to true>>
<<jump JohnnyQuestions>>
===

title: JohnnyKnowsDale
position: 499,-164
---
Detective: You must know Dale Butterbur very well if you have a share in the bar?
Johnny: I do. Real straight-edge guy, but a clever businessman.

<<jump JohnnyQuestions>>
===

title: JohnnyKnowsLoretta
position: 495,-14
---
Detective: How well do you know Loretta Dubois?
Johnny: That little canary? I try to avoid her. A broad like her is only after one thing - moolah, and lots of it. Doubt she would’ve hung around Harlow so much if she’d known he didn’t have a checker to his name.

<<jump JohnnyQuestions>>
===

title: JohnnyKnowsPeggy
position: 358,167
---
Detective: Do you know much about Peggy Harlow?
Johnny: Not a thing. She seems like one of those prim and proper broads, not the type I associate with.

<<jump JohnnyQuestions>>

===
title: AboutHorseTickets
position: 495,-14
---
Detective: I found a debt stub for a horse bet Mr. Harlow had lost, looks like he owed someone a fair bit of money. You wouldn’t happen to know anything about that, would you?
Johnny: Maybe. We hold what we call a “games” night here for the regulars every now and then. Racers, poker, that sorta thing. Jack likes to bet on the ponies, but he ain’t got squat to bet with.
Detective: Then it sounds like you’re the one he owes money to. And he can’t pay, that must make you mad.
Johnny: Mad enough to shoot him, right? That’s what you’re getting at, ain’t you, Detective? Well, nice try, but I ain’t done nothing. But honestly, between you and me, I’m upset it weren’t me what got to fill the bastard with lead. Jack Harlow got what he deserved.
<<set $DetectiveHasSpokenToJohnnyHorseTicket to true>>

<<jump JohnnyQuestions>>
===
title: AboutTheBloodInTheLadies
position: 495,-14
---
Detective: I have a witness who can place you in the *ladies* bathroom at the time of the murder.
Johnny: Our little canary squawked, did she? Well, so what? If I was in the bathroom, I weren’t out shooting that pill Harlow, was I?
Detective: You said you were in the men’s bathroom. You lied.
Johnny: Well I didn’t want to come right out and say I was busy roughing up a broad, did I? But I suppose she told you about all that then. Her fella owed me money, he weren’t paying. So I intimidated her a bit, is all. It’s just business.
Detective: Her “fella”?
Johnny: Harlow, of course. He was dizzy with that dame. I heard he pawned his wedding ring just to buy her new jewelry. Rather spend money on his new flame than on his debts, the swine.

<<set $DetectiveHasSpokenToJohnnyBlood to true>>

<<jump JohnnyQuestions>>
===
title: AboutTheFlaskAndDale
position: 495,-14
---
Detective: It’s interesting to me that even though you clearly disliked Jack Harlow, your business partner Butterbur allowed Jack to bring his own booze into the bar. Jack owes you money and he doesn’t even buy his own hooch at the bar.
Johnny: It’s interesting all right. I guess that everytime Jack’s here drinking, that broad of his is here too to pick his sorry ass up. 
Detective: And?
Johnny: Alls I’m saying is she’s around here a lot, and Dale doesn’t seem to mind.

<<set $DetectiveHasSpokenToJohnnyDebt to true>>

<<jump JohnnyQuestions>>
===

title: JohnnyInterrogate
position: 495,-14
---
Detective: So you admit to beating on the woman Jack was seeing, running “games” nights that sound an awful lot like an unlicensed gambling ring, and admit that Jack owed you a lot of money. I gotta say, Johnny, the circumstantial evidence is pointing in your direction…
Johnny: Is that so?
Detective: If Jack wasn’t paying up, and neither was his lady, maybe there was only one way to get him out of your hair for good.
Johnny: You don’t quit, do ya? Listen, Gumshoe, either provide the hard proof and arrest me or let me go home, alright?

<<set $DetectiveHasInterrogatedJohnny to true>>

<<jump JohnnyQuestions>>
===

title: JohnnyGoodbye
position: -191,263
---

<<set $johnnyRandomGoodbye to random_range(0,2)>>
<<if $johnnyRandomGoodbye == 0 >>
Detective: That’s all I need for now.
Johnny: Good.
<<elseif $johnnyRandomGoodbye == 1>>
Detective: I appreciate the help.
Johnny: Yeah, yeah.
<<else>>
Detective: Thank you, Mr. Malone.
Johnny: Just let me know when I can go home.
<<endif>>

<<stop>>
===