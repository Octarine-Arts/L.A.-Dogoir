title: PeggyHarlowDialogue
tags:NPC, Detective
position: -338,-113
---
<<if $PeggyIsInitialGreeting == false>>

Peggy: I suppose you think it was me, don’t you, Detective?
Detective: And why would you say that?
Peggy: It’s always the wife or husband, isn’t that true?
Detective: Perhaps you can tell me that.
Peggy: I’ve just lost my husband, Detective. Jack wasn’t John Wayne by any means, but I loved him. I’ve read enough dime store detective novels to know you detectives always look at the spouse, but I can assure you I would never do anything to harm my Jack. I don’t know what I’ll do without him.
<<set $PeggyIsInitialGreeting to true>>

<<elseif $PeggyIsInitialGreeting == true>>

<<set $PeggyRandomGreeting to random_range(0,2)>>
<<if $PeggyRandomGreeting == 0 >>
<<Animate "Wave">>
Peggy: More interrogation, Detective?
<<elseif $PeggyRandomGreeting == 1>>
Peggy: Yes?
<<elseif $PeggyRandomGreeting == 2>>
Peggy: Hello again, Detective.
<<endif>>

<<endif>>

<<jump PeggyQuestions>>
===
title: PeggyQuestions
position: 69,-84
---
-> Detective: Who are you?
<<jump PeggyWhoAreYou>>

-> Detective: Gunshot?
<<jump PeggyGunshot>>

-> Detective: Come here often?
<<jump PeggyComeHereOften>>

-> Detective: Jack Harlow?
<<jump PeggyDoYouKnowJack>>

-> Detective: Enemies?
<<jump PeggyWhoWantsJackDead>>

<<if $Dale == true>> 
<<if $DetectiveHasFoundSyringe == false>>
<<if $PeggySpokenAboutGunshot == true>>
-> Detective:  Dale?
<<jump PeggyKnowsDale>>
<<endif>>
<<endif>>
<<endif>>

<<if $Loretta == true>> 
-> Detective:  Loretta?
<<jump PeggyKnowsLoretta>>
<<endif>>

<<if $Johhny == true>> 
-> Detective:  Johnny?
<<jump PeggyKnowsJohhny>>
<<endif>>

<<if $JohnnySawPeggyFromAlley == true>>
-> Detective: Johnny saw you outside
<<jump PeggySeenOutside>>
<<endif>>

<<if $DetectiveHasFoundTickets == true>>
-> Detective:  These tickets I've found
<<jump TicketsToKingKong>>
<<endif>>

<<if $DetectiveHasFoundMissingRing == true>>
-> Detective:  Jack doesn't wear his ring anymore?
<<jump WhereIsTheRing>>
<<endif>>

<<if $DetectiveHasFoundFlask == true>>
-> Detective:  Flask to a bar?
<<jump AboutTheFlask>>
<<endif>>

<<if $DetectiveHasFoundSyringe == true>>
<<if $PeggySpokenAboutAlley == true>>
-> Detective:  Drugs in the alley?
<<jump AboutTheSyringe>>
<<endif>>
<<endif>>

<<if $DetectiveHasSpokenToPeggySyringe == true>>
-> Detective:  Why did you hide it?
<<jump WhyDidYouHideIt>>
<<endif>>

-> Detective: Goodbye
<<jump PeggyGoodbye>>

===

title: PeggyWhoAreYou
position: 283,-340
---
Detective: Can you tell me your name please, ma’am?
Peggy: Peggy Harlow. Am I still a Harlow now that Jack’s gone?
<<jump PeggyQuestions>>
===

title: PeggyGunshot
position: -111,-477
---
Detective: Where were you when the gunshot went off?
Peggy: I was sitting right here. The lights had just gone out. Scared me something horrid, I got up to help Dale- Mr. Butterbur, I mean - with the lights.

<<set $Peggy to true>>
<<set $PeggySpokenAboutGunshot to true>>
<<jump PeggyQuestions>>
===
title: PeggyComeHereOften
position: 252,-520
---
Detective: Do you come to the bar with your husband often?
Peggy: Goodness me, no. Not if I can avoid it. An establishment like this is no place for a lady. Jack just…loses track of time, sometimes. I come to…collect him, occasionally. When he forgets.

<<set $Peggy to true>>
<<jump PeggyQuestions>>
===

title: PeggyWhoWantsJackDead
position: 372,-273
---
Detective: Did your husband have any enemies, Mrs. Harlow?
Peggy: Jack wasn’t the easiest man to get along with, but I couldn’t imagine he would have enemies!

<<set $Peggy to true>>
<<jump PeggyQuestions>>
===

title: PeggyKnowsDale
position: 360,-36
---
Detective: How well do you know the bartender, Mr. Butterbur?
Peggy: Oh, not very well at all.
Detective: And yet you referred to him by his first name - Dale.
Peggy: Well - yes, coming to collect Jack as I do, we have spoken a few times. He seems like a very decent man, as far as bar owner’s go.

<<jump PeggyQuestions>>
===

title: PeggyKnowsLoretta
position: 368,82
---
Detective: Do you know much about Loretta Dubois?
Peggy: I know she’s no respectable woman.
Detective: And why is that? 
Peggy: Forgive me for being harsh, Detective, but that woman is a golddigger and nothing more.

<<jump PeggyQuestions>>
===

title: PeggyKnowsJohhny
position: 264,227
---
Detective: Are you familiar with Johnny “Two Sleeves” Malone:
Peggy: Not at all, but even so he makes me quite uncomfortable. He seems like a real thug.

<<jump PeggyQuestions>>

===

title: PeggySeenOutside
position: 264,227
---
Detective: You were seen running in from the back alley when the lights turned back on - any reason you were outside?
Peggy: I’m not sure why that matters, Detective. I could have hardly murdered my husband from outside. Surely that proves my innocence.
Detective: Just answer the question, Mrs. Harlow.
Peggy: Can’t a woman go out for a breath of fresh air?

//
<<set $PeggySpokenAboutAlley to true>>
<<jump PeggyQuestions>>

===
title: TicketsToKingKong
position: 368,82
---
Detective: I know this might seem irrelevant, ma’am, but did you and your husband go to see King Kong last night?
Peggy: No… we didn’t. I was at home last night. Jack…well, I assumed Jack was here in the bar.
Detective: Could he have been at the cinema with someone else? I found two King Kong tickets in your husband's pocket, you see.
Peggy: I know nothing about that, Detective. I suppose you should go and ask that…that woman who calls herself an actress.
 
<<set $DetectiveHasSpokenToPeggyTickets to true>>

<<jump PeggyQuestions>>
===
title: WhereIsTheRing
position: 368,82
---
Detective: I’m sorry I have to ask this, ma’am, but I noticed your husband didn’t have a wedding ring on his finger…?
Peggy: I see what you’re insinuating, Detective. I’m not ashamed to admit that we had problems. But what marriage does not have its problems, for goodness sake?
Detective: But I see you’re still wearing your ring?
Peggy: Of course. Jack just… well, men deal with their problems differently, I suppose.

<<set $DetectiveHasSpokenToPeggyMissingRing to true>>

<<jump PeggyQuestions>>
===
title: AboutTheFlask
position: 368,82
---
Detective: I found a flask on your husband. Did he drink a lot?
Peggy: Jack had recently been laid off at work, I think he found it cheaper to simply bring in his own alcohol rather than buy drinks here. He told me Dale was fine with it. 

<<set $DetectiveHasSpokenToPeggyFlask to true>>

<<jump PeggyQuestions>>
===
title: AboutTheSyringe
position: 368,82
---
Detective: The reason I ask about you being in the alleyway, Mrs. Harlow, is that there was a syringe out there that appears to have been recently used. I don’t suppose you know anything about that?
Peggy: You don’t give up, do you Detective? Very well, I did use the syringe. Understand that when Jack drinks too much he can get awfully violent. It was just something to get him to sleep. I wanted to get him home quietly, without any issues. Dale said he had the perfect thing and that he’d help get him into a taxi one he was sleeping. I don’t know, perhaps if I hadn’t given it to him he wouldn’t have been there to get shot? Could that have changed how things turned out? Oh God, would he still be alive if he had been awake?

<<set $DetectiveHasSpokenToPeggySyringe to true>>

<<jump PeggyQuestions>>
===
title: WhyDidYouHideIt
position: 368,82
---
Detective: Why did you try to hide the syringe?
Peggy: I was just so scared. With the lights going out and then the gunshot…I don’t know Detective, I just panicked! How would it look, a respectable wife using drugs on her own husband? 

<<jump PeggyQuestions>>
===

title: PeggyGoodbye
position: -191,263
---

<<set $peggyRandomGoodbye to random_range(0,2)>>
<<if $peggyRandomGoodbye == 0 >>
<<Animate Dale "GoodbyeWave">>
Detective: Thank you for your time, Mrs. Harlow.
Peggy: May I grieve in peace, now?
<<elseif $peggyRandomGoodbye == 1>>
<<Animate Dale "GoodbyeWave">>
Detective: That’ll be all, ma’am.
Peggy: Find Jack’s murderer, Detective. 
<<elseif $peggyRandomGoodbye == 2>>
<<Animate Dale "GoodbyeWave">>
Detective: Thanks.
Peggy: Farewell.
<<endif>>

<<stop>>
===