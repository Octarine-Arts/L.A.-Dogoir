title: AlleyCatDialogue
tags:NPC, Detective, Dog
position: -338,-113
---
<<if $OMalleyIsInitialGreeting == false>>
    OMalley: I don’t do the whole dog-cat thing so don’t even try, pal. I could eat a mutt like you for breakfast.
    Archie: I’m just here to ask you some questions, sir, about the gunshot that sounded earlier.
    OMalley: Oh. I don’t know much about that.
    <<set $OMalleyIsInitialGreeting to true>>
<<elseif $OMalleyIsInitialGreeting == true>>
    <<set $OMalleyRandomGreeting to random_range(0,2)>>
    <<if $OMalleyRandomGreeting == 0>>
        OMalley: What do you want now? 
    <<elseif $OMalleyRandomGreeting == 1>>
        OMalley: You don’t quit, do you? 
    <<else>>
        OMalley: Can’t you see I’m busy? 
    <<endif>>
<<endif>>

<<jump OMalleyQuestions>>

===
title: OMalleyQuestions
position: 69,-84
---
-> Detective: Who are you?
    <<set $ArchieFoundTheCheese to true>>
    <<jump OMalleyWhoAreYou>>


-> Detective: Gunshot? <<if $OMalleyAskedForName == true>> 
    <<jump OMalleyGunshot>>


-> Detective: What did they throw at you?? <<if $OMalleyAskedAboutThrow == true>> 
    <<jump OMalleyWhatDidTheyThrow>>


-> Detective: Can you get it? <<if $OMalleyAskedGetIt == true>> 
    <<jump OMalleyCanYouGetIt>>


-> Detective: Goodbye
    <<jump OMalleyGoodbye>>

===

title: OMalleyWhoAreYou
position: 283,-340
---
Archie: You got a name?
OMalley: Yeah, O’Malley.

<<set $OMalleyAskedForName to true>>

<<jump OMalleyQuestions>>
===

title: OMalleyGunshot
position: -111,-477
---

Archie: Were you out here when the gunshot rang out, Mr. O’Malley?
OMalley: Well they don’t let me inside, do they? Only you dogs get that pleasure. Yeah, I was here. I was trying to dig through these bins, find a little dinner. Next thing you know...
OMalley: KA-BOOM! 
OMalley: Scared the hairballs out of me… finally got back to the task at hand and some damn human comes out here and throws something at me. What a night.
Archie: Some human?
OMalley: I don’t know, I was a bit dazed on account of their aim. Only just saw the back of them as they went back inside.

<<set $OMalleyAskedAboutThrow to true>>
<<jump OMalleyQuestions>>

===

title: OMalleyWhatDidTheyThrow
position: -111,-477
---

Archie: What did they throw at you?
OMalley: A little tube or something, I don’t know. It’s in this here bin somewhere.
<<set $OMalleyAskedGetIt to true>>
<<jump OMalleyQuestions>>
===

title: OMalleyCanYouGetIt
position: -111,-477
---

Archie: Can you get it for me?
OMalley: Now why would I do extra work for something that got nothing to do with me?
Archie: I’ll get it myself then.
OMalley: I don’t think so. This is my turf, pal. I’ve answered your dumb questions real nice, so how about you leave me alone?

<<set $OMalleyWillNotGetSyringe to true>>
//player needs to lead cat away with rat

<<jump OMalleyQuestions>>
===

title: OMalleyGoodbye
position: -191,263
---

<<set $OMalleyRandomGoodbye to random_range(0,2)>>
<<if $OMalleyRandomGoodbye == 0 >>
    Archie: Thank you.
    OMalley: Sure.
<<elseif $OMalleyRandomGoodbye == 1>>
    Archie: See you around.
    OMalley: Preferably not.
<<else>>
    Archie: I’ll be back if I have more questions.
    OMalley: Can’t wait…
<<endif>>

<<stop>>
===