title: FrouFrouDialogue
tags:NPC, Detective, Dog
position: -338,-113
---
<<if $FrouFrouIsInitialGreeting == false>>
    FrouFrou: Goodness Gracious, what breed are you, darling, you look positively novel! No, don’t tell me, let me guess…. You are a… pointer of some kind?
    Archie:  I’m a labrador, actually.
    FrouFrou: A labrador! How delightful! I’m not used to conversing with what you might call lesser breeds, you see. Not that you seem lesser, darling! You seem simply enchanting.
    <<set $FrouFrouIsInitialGreeting to true>>
<<endif>>

<<jump FrouFrouQuestions>>

===
title: FrouFrouQuestions
position: 69,-84
---
-> Archie: Who are you?
    <<jump FrouFrouWhoAreYou>>

-> Archie: Gunshot?
    <<jump FrouFrouGunshot>>

-> Archie: Do you know the murdered man?
    <<jump FrouFrouDoYouKnowHim>>

-> Archie: Doing anything later?
    <<jump FrouFrouAskingOut>>

-> Archie: What were you doing there? <<if $HidInOffice == true>>
    <<jump FrouFrouWhatWereYouDoingInTheOffice>>

-> Archie: Heathen man? <<if $HeathenMan == true>>
    <<jump FrouFrouHeathenMan>>

-> Archie: Did you see or hear anything? <<if $Office == true>>
    <<jump FrouFrouSeeOrHearAnything>>

-> Archie: Goodbye
    <<jump FrouFrouGoodbye>>

===

title: FrouFrouWhoAreYou
position: 211,-477
---
Archie: What is your name, ma’am?
FrouFrou: Frou-Frou, darling, Frou-Frou Dubois. That’s “miss” Dubois, by the by.


<<jump FrouFrouQuestions>>
===

title: FrouFrouGunshot
position: -300,-305
---

Archie: Where were you when the gunshot rang out?
FrouFrou: Oh yes, that frightful business. I was in the office.
<<set $HidInOffice to true>>
<<jump FrouFrouQuestions>>

===

title: FrouFrouWhatWereYouDoingInTheOffice
position: -248,-453
---

Archie: What were you doing in the office?
FrouFrou: I had been in the ladies room with my Mistress - she and I are rarely separated, you see - when that heathen man came in. How I despise him! So as he entered I slipped out the door and hid in the office. 
FrouFrou: Then with all that happened afterwards… well, I was just too scared to move! Perhaps I am a coward for not being with my Mistress, but there is something ever so sinister about him. 
FrouFrou: ‘Tis a shame I have no strong, brave dog in my life, such as yourself, else I might not feel so afraid. 

<<set $HeathenMan to true>>
<<set $Office to true>>
<<jump FrouFrouQuestions>>
===
title: FrouFrouHeathenMan
position: -39,-503
---

Archie: Who is the “heathen man” you’re referring to?
FrouFrou: That brute in the corner with the scar on his face. He is simply frightful.

<<jump FrouFrouQuestions>>
===
title: FrouFrouSeeOrHearAnything
position: -39,-503
---

Archie: Did you happen to see, or hear anything from the office? 
FrouFrou: Well…no, not particularly. I’m ashamed to admit that at that deafening sound, and with the business of the lights going out, I was rather shaken. I stayed in the office even after that servant man Butterbur came in and turned on the lights.

<<jump FrouFrouQuestions>>
===

title: FrouFrouDoYouKnowHim
position: -39,-503
---

Archie: Does your mistress have any connection to the murdered man?
FrouFrou: Yes, though we should hardly associate with one so beneath our pedigree. Often he came to our residence. I hated it. 
FrouFrou:I sleep on a silk pillow beside my Mistress every night without fail, except when *he* shows up. Then I am locked out of the bedroom! Did you ever hear of such injustice?

<<jump FrouFrouQuestions>>
===

title: FrouFrouAskingOut
position: -39,-503
---

Archie: So...after I solve this murder...do you fancy going out for a milkbone? We could probably find one around here somewhere. 
FrouFrou: Oh, Detective! I would love to but I'm on a special diet. I don't eat bones - unlike those brutish gamblers out in the alleyway...
Archie: ...What about a romantic spaghetti dinner?
FrouFrou: I could be convinced. Come talk to me after you've solved the case. 


<<jump FrouFrouQuestions>>
===

title: FrouFrouGoodbye
position: 203,212
---

<<set $FrouFrouRandomGoodbye to random_range(0,2)>>
<<if $FrouFrouRandomGoodbye == 0 >>
    Archie: Thank you for your time, ma’am.
    FrouFrou: No thank *you*, darling.
<<elseif $FrouFrouRandomGoodbye == 1>>
    Archie: That’ll be all.
    FrouFrou: Toodles!
<<else>>
    Archie: Thanks for your help.
    FrouFrou: Ta-ta, darling!
<<endif>>

<<stop>>
===