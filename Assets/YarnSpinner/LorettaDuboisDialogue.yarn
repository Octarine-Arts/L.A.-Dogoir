title: LorettaDuboisDialogue
tags:NPC, Detective
position: -338,-113
---
<<if $LorettaIsInitialGreeting == false>>
    Loretta: I’m much too upset about current events to talk, Detective. This is just too much for my nerves!
    Detective: I understand that it must be difficult, ma’am, but I really must insist you answer a few questions. I have to find out everything I can if we’re to catch the murderer.
    Loretta: Very well. I’m an actress you know, I can master my emotions under any circumstances. Even ones as trying as these.
    <<set $LorettaIsInitialGreeting to true>>
<<elseif $LorettaIsInitialGreeting == true>>
    <<set $LorettaRandomGreeting to random_range(0,3)>>
    <<if $LorettaRandomGreeting == 0 >>
        Loretta: Goodness gracious, what else do you want from me?
    <<elseif $LorettaRandomGreeting == 1>>
        Loretta: Come back to interrogate me more, have you?
    <<elseif $LorettaRandomGreeting == 2>>
        Loretta: What now, Detective?
    <<else>>
        Loretta: Can’t a woman grieve in peace?
    <<endif>>
<<endif>>

<<jump LorettaQuestions>>

===
title: LorettaQuestions
position: 69,-84
---
-> Detective: Who are you?
    <<jump LorettaWhoAreYou>>

-> Detective: Gunshot?
    <<jump LorettaGunshot>>

-> Detective: Come here often?
    <<jump LorettaComeHereOften>>

-> Detective: Jack Harlow?
    <<jump LorettaDoYouKnowJack>>

-> Detective: Dale? <<if $Dale == true>> 
    <<jump LorettaKnowsDale>>

-> Detective: Closest to the Victim? <<if $DaleKnowsAboutLoretta == true>> 
    <<jump DaleKnowsAboutLoretta>>
 
-> Detective: Peggy? <<if $Peggy == true>>
    <<jump LorettaKnowsPeggy>>

-> Detective: Johnny? <<if $Johnny == true>> 
    <<jump LorettaKnowsJohhny>>

-> Detective:  Dale's gun? <<if $DaleHasSpokenAboutShotgun == true && $DetectiveHasFoundGun == false>>
    <<jump AboutThePistol>>

-> Detective:  Seen the bathroom lately? It's a real bloodbath in there <<if $LorettaSpokenAboutLocation == true && $DetectiveHasFoundBlood == true>>
    <<jump AboutBathroomBlood>>

-> Detective:  How were the movies? <<if $DetectiveHasSpokenToPeggyTickets == true>>
    <<jump KingKongTickets>>

-> Detective: Goodbye
    <<jump LorettaGoodbye>>

===

title: LorettaWhoAreYou
position: 283,-340
---
Detective: Can you please tell me your name, ma’am?
Loretta: Loretta Dubois. That’s D-U-B-O-I-S, make sure you spell it right in your little journal. 

<<jump LorettaQuestions>>
===

title: LorettaGunshot
position: -111,-477
---
Detective: Where were you when the murder took place?
Loretta: I was in the ladies room, touching up my makeup.

<<set $Loretta to true>>
<<set $LorettaSpokenAboutLocation to true>>
<<jump LorettaQuestions>>
===
title: LorettaComeHereOften
position: 252,-520
---
Detective: You don’t often see women in establishments like this, do you come here often?
Loretta: I’m a performer, I sing here most weeks. It’s something to fill my time while I wait for Hollywood to come knocking. Which shouldn’t be too much longer, mind you.

<<set $Loretta to true>>
<<jump LorettaQuestions>>
===

title: LorettaDoYouKnowJack
position: 372,-273
---
Detective: Did you know Jack Harlow well?
Loretta: Not particularly well… he was a regular here, that much I knew. He would watch me perform, he seemed to have an appreciation for my music. Most people do, of course.

<<set $Loretta to true>>
<<jump LorettaQuestions>>
===

title: LorettaKnowsDale
position: 360,-36
---
Detective: What do you think of Dale Butterbur?
Loretta: He’s a fine man, a good employer. 

<<jump LorettaQuestions>>

===
title: DaleKnowsAboutLoretta
position: 360,-36
---
Detective: Dale Butterbur says you were the closest to the victim when he returned from restoring the lights. Impressive journey to make in the dark from the bathroom.
Loretta: What on earth are you insinuating, Detective? You think that I killed a man in cold blood? Just when my career is about to take off? What on earth would I do such a thing for! The very idea!
Detective: Just doing my job, Miss.

<<jump LorettaQuestions>>
===

title: LorettaKnowsPeggy
position: 368,82
---
Detective: Do you know Peggy, Mr. Harlow’s wife?
Loretta: No.

<<jump LorettaQuestions>>
===

title: LorettaKnowsJohhny
position: 264,227
---
Detective: How well do you know Johnny Malone?
Loretta: Well enough to know he is a grifter and a gangster and an all-round horrible man. If anyone is to blame for Jack’s murder, it’s him!


<<jump LorettaQuestions>>

===
title: AboutThePistol
position: 368,82
---
Detective: Mr. Butterbur said he gave you a gun, is that true?
Loretta: Yes, he did. Alas! I lost it about a month ago.

<<jump LorettaQuestions>>
===

title: AboutBathroomBlood
position: 368,82
---
Detective: You told me you were in the bathroom when the murder occured, but you failed to mention the blood on the floor in there? I can’t imagine you missed it.
Loretta: Oh, that. Uh, yes, well I did see it. Of course I did. I just was so flustered about the murder when you asked. It must have…slipped my mind.
Detective: Miss Dubois…
Loretta: Yes, yes, alright. That blood…is mine. That thug “Two Sleeves” or whatever ridiculous thing he calls himself… He came in while I was in there. He said something about money Jack owed, said that I should pay up since Jack couldn’t. 
Loretta: I mean, what nonsense! I don’t owe anything. So I told him that very thing. Well, being the brute he is he hit me. But I’m fine, Detective, don’t worry about me. Unlike poor Jack I lived to tell the tale.

<<set $DetectiveHasSpokenToLorettaBlood to true>>

<<jump LorettaQuestions>>
===

title: KingKongTickets
position: 368,82
---
Detective: What did you think of King Kong when you saw it last night?
Loretta: Oh, I loved it! It was a little silly but I just adore Fay Wray. She is très magnifique! Wait...how do you know I was…
Detective: Did Jack take you to the movies often, Miss Dubois?
Loretta: I, uh…I don’t know what you’re talking about.
Detective: Miss Dubois…there’s no point acting dumb with me now. I know the two of you went to the movies last night. He had two tickets in his pocket and his wife didn’t go with him.
Loretta: It was rather rude of you to trick me into admitting it like that, Detective. I suppose you think you’re awfully clever.
Detective: Just a little. But perhaps it’s time to come clean about your relationship with Jack Harlow.
Loretta: Very well, Detective. Jack and I were…friendly with one another. But is that such a crime?
Detective: Mr. Harlow had a wife, so it just might be a crime, depending on how friendly you were.
Loretta: Alright, alright. Yes, Jack and I were seeing each other. But obviously this proves my innocence. I wouldn’t kill the man I love, would I?
Detective: So you loved him?
Loretta: Yes. I did. Jack could be severe. But he was straight-forward and honest. And he was certainly better than a lot of the men out there. Especially the ones in show business. So yes, Detective, I was seeing him. But I didn’t want him dead. Perhaps instead of scrounging around in my personal affairs, you go and find out who really killed him.


<<jump LorettaQuestions>>
===

title: LorettaGoodbye
position: -191,263
---

<<set $lorettaRandomGoodbye to random_range(0,2)>>
<<if $lorettaRandomGoodbye == 0 >>
Detective: Thank you for your time.
Loretta: Do your job detective
<<elseif $lorettaRandomGoodbye == 1>>
Detective: That’s all for now, ma’am.
Loretta: Just find whoever did this detective
<<else>>
Detective: Thank you for your cooperation
Loretta: Anything to find the murderer.
<<endif>>

<<stop>>
===