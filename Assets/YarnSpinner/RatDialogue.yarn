title: RatDialogue
tags:NPC, Detective, Dog
position: -338,-113
---
<<if $RatIsInitialGreeting == false>>
    Rattus: What the hell d’you want?
    Archie: Pleased to meet you, too. I’d like to ask you some questions about the murder that’s just taken place.
    Rattus: Hmmph.
    <<set $RatIsInitialGreeting to true>>
<<elseif $RatIsInitialGreeting == true>>
    <<set $RatRandomGreeting to random_range(0,2)>>
    <<if $RatRandomGreeting == 0>>
        Rattus: You again?
    <<elseif $RatRandomGreeting == 1>>
        Rattus: What?
    <<else>>
        Rattus: What now, Poochy?
    <<endif>>
<<endif>>

<<jump RatQuestions>>

===
title: RatQuestions
position: 69,-84
---
-> Detective: Who are you?
    <<jump RatWhoAreYou>>

-> Detective: Gunshot?
    <<jump RatGunshot>>

-> Detective: Did you see anything?
    <<jump RatDidYouSeeAnything>>

-> Detective:  I've got some cheese for you <<if $ArchieFoundTheCheese == true && $RattusWantsCheese>>
    <<jump RatFoundTheCheese>>

-> Detective:  I saw some cheese outside... <<if $ArchieGaveCheese == true && $OMalleyWillNotGetSyringe == true>> 
    <<jump RatSawCheeseOutside>>

-> Detective:  What are you doing out here? <<if $RattusMovedOutside == true>> 
    <<jump RatMovedToBin>>

-> Detective: Goodbye
    <<jump RatGoodbye>>

===

title: RatWhoAreYou
position: 283,-340
---
Archie: Who are you, exactly?
Rattus: I’m Rattus and Rattus is me.

<<jump RatQuestions>>
===

title: RatGunshot
position: -111,-477
---

Archie: Where were you when the shot rang out, Rattus?
Rattus: Rattus was right ‘ere.

<<jump RatQuestions>>

===

title: RatDidYouSeeAnything
position: -111,-477
---

Archie: If you were here, you must have seen something?
Rattus: You sure ask a lotta questions. Maybe Rattus saw somethin’, maybe he didn’t. What’s in it for him if he tells you what he saw? Tell ya what, I’m awfully hungry. Find Rattus some food and then maybe he’ll talk.
Archie: Some food?
Rattus: Yeah, yeah… some cheese would be nice. Run along now, little pet.
<<set $RattusWantsCheese to true>>

<<jump RatQuestions>>
===

title: RatFoundTheCheese
position: -111,-477
---

Archie: I got you some cheese like you asked.
Rattus: Give it ‘ere!
Archie: Not so fast. Tell me what you know, and then I’ll give you the cheese.
Rattus: Hmph. Fine. Rattus saw two of them humans, saying somethin’ about a safe.
Archie: A safe? What exactly did they say?
Rattus: They was talkin’ about the code on account of one of them forgettin’. 
Archie: And? Did they say what the code was?
Rattus: Yeah, it was BDFA.
Archie: Thanks for your help, Rattus, here’s the cheese.
Rattus: ‘Bout time. Give it ‘ere!

<<TradeItem Yarn "Cheese">>
<<set $ArchieGaveCheese to true>>
<<jump RatQuestions>>
===
title:  RatSawCheeseOutside
position: -111,-477
---
Archie: Hey, I saw some more cheese outside…
Rattus: Really? 
Archie: Yeah, a whole wheel. You should have a look.
Rattus: Outta the way!

//make rattus move to outside
<<StartAnim AnimatorRat>>
<<set $RattusMovedOutside to true>>
<<stop>>

===
title:  RatMovedToBin
position: -111,-477
---
Archie: Uh…
Rattus: Surprised to see me, aren’t ya, pup? Betcha thought your little lie about the cheese would lead ol’ Rattus to his death, didn’t ya? There ain’t no cat Rattus can’t lick, no sirree! 
Rattus: I should thank ya, really, first a bit of cheese and now a good fight! Rattus hasn’t felt so alive in weeks!
Archie: Uh…glad to help. Is the cat ok?
Rattus: Rattus sent that ol’ puss howlin’ all the way home! Hehehe.

<<wait 2>>

Archie: Did the cat leave anything behind?
Rattus: Left this ‘ere stick thing.
Archie: May I have it?
Rattus: Well Rattus sure don’t want it.
<<PromptPlayer Yarn "I better let my partner know about this bit of evidence..." 1>>
<<jump RatQuestions>>
===

title: RatGoodbye
position: -191,263
---

<<set $RatRandomGoodbye to random_range(0,2)>>
<<if $RatRandomGoodbye == 0>>
Archie: Thanks.
Rattus: Whatever.
<<elseif $RatRandomGoodbye == 1>>
Archie: That’s all for now.
Rattus: Only for now? Drat.
<<else>>
Archie: I appreciate it.
Rattus: Well I don’t.
<<endif>>

<<stop>>
===