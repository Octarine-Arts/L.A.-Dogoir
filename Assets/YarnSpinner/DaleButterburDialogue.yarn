title: DaleButterburDialogue
tags:NPC, Detective
position: -522,-132
---
<<if $DaleIsInitialGreeting == false>>
    Dale: I can’t believe this is happening in my bar. Whatever you need, Detective, you just let me know.
    Detective: I’d like to ask you some questions, actually.
    Dale: Of course.
    <<set $DaleIsInitialGreeting to true>>
<<elseif $DaleIsInitialGreeting == true>>
    <<set $DaleRandomGreeting to random_range(0,3)>>
    <<if $DaleRandomGreeting == 0>>
        Dale: How can I help, Detective?
    <<elseif $DaleRandomGreeting == 1>>
        Dale: Any leads, Detective?
    <<elseif $DaleRandomGreeting == 2>>
        Dale: What do you need?
    <<else>>
        Dale: How’s the investigation going?
    <<endif>>
<<endif>>

<<jump Questions>>

===
title: Questions
position: -307,-131
---
-> Detective: Who are you?
    <<jump WhoAreYou>>

-> Detective: Gunshot?
    <<jump Gunshot>>

-> Detective: Jack Harlow?
    <<jump DoYouKnowJack>>

-> Detective: Enemies?
    <<jump WhoWantsJackDead>>
 
-> Detective:  Peggy? <<if $Peggy == true>>
    <<jump Peggy>>

-> Detective:  Loretta? <<if $Loretta == true>> 
    <<jump Loretta>>

-> Detective:  Johnny? <<if $Johnny == true>> 
    <<jump Johhny>>

-> Detective: The door? <<if $DetectiveTriedToOpenDoor == true>>
    <<jump office>>

-> Detective: About the shotgun <<if $DetectiveHasFoundShotgun == true>>
    <<jump Shotgun>>

-> Detective: I found the drugs Dale <<if $DetectiveHasFoundDrugs == true>>
    <<jump Drugs>>

-> Detective: Peggy visits often? <<if $DetectiveHasFoundFlask == true && $JohnnyHasSpokenAboutFlask == true>>
    <<jump PeggyVisitsOften>>

-> Detective: Why do you have a picture of Peggy? <<if $DaleHasSpokenAboutPeggy == true && $DetectiveHasFoundPicture == true>>
    <<jump PhotoOfPeggy>>

-> Detective: Goodbye
    <<jump Goodbye>>

===

title: WhoAreYou
position: 204,-140
---
Detective: Who are you, exactly?
Dale: Dale Butterbur, I own this bar. It was used by bootleggers during the prohibition, would you believe, but the old owner went to the big house for brewing his own hooch, and viola, it was mine for the buying, right as the prohibition ended.

<<jump Questions>>
===

title: Gunshot
position: -77,-430
---
Detective: Tell me, exactly where you were when the gunshot occurred?
Dale: The lights had just gone out, so I was in the storeroom turning the breaker back on. Faulty wiring, it trips every now and then.

<<jump Lights>>
===
title: Lights
position: 237,-418
---
Detective: When you turned the lights back on, did you see anyone around the body, anyone acting suspiciously?
Dale: Now that you mention it, Loretta was the closest to Harlow when I came back out. That was odd as she hadn’t been in the room when I left.

<<set $Dale to true>>
<<set $DaleKnowsAboutLoretta to true>>
<<jump Questions>>
===

title: DoYouKnowJack
position: 191,-236
---
Detective: How well do you know Jack Harlow?
Dale: Not particularly well. He was a semi-regular here, I guess you could say. But to be honest with you detective, he was a real drip. A real greaseball, from what I could tell. Never liked to serve him, but a few lincolns is a few lincolns, no matter the customer.

<<set $Dale to true>>
<<jump Questions>>
===
title: WhoWantsJackDead
position: 216,-49
---
Detective: Do you know of anyone here who would want Jack Harlow dead?
Dale: Well, he didn’t seem like a stand-up gent in general. But I just can’t imagine someone would want to kill him, even so.

<<set $Dale to true>>
<<jump Questions>>
===

title: Peggy
position: 209,53
---
Detective: How well do you know Peggy Harlow?
Dale: I hardly know her at all. She comes in sometimes to pick up her drunk excuse for a husband. I felt sorry for her, honestly.

<<jump Questions>>
===

title: Loretta
position: 171,192
---
Detective: What do you know about Loretta Dubois?
Dale: She performs here once a week - she’s a great singer. Obviously she’s a bit of a dish too, which brings in the customers. I think Harlow got it for her bad. Never missed a performance. He was more likely to pay her just for existing than he was to pay me for booze.

<<jump Questions>>
===

title: Johhny
position: 181,302
---
Detective: What’s the deal with Johnny Malone?
Dale: I know he looks rough, but he’s a swell guy. He’s got a little share in the bar with me, so we’re sort of like business partners. He’s my security on busier nights. He’s got such a sour puss people tend to behave when he’s around. Until now, I guess…

===
title: office
position: 141,457
---
Detective: Can you let me into the office? I’d like to look around.
Dale: I’d love to help, Detective, but I’m a little busy right now. I’ll open it up when I’ve cleaned up the mess here.

<<jump Questions>>
===
title: Shotgun
position: 141,457
---
Detective: That’s a helluva big gun you’ve got behind the bar there, Mr. Butterbur.
Dale: She’s a beauty, isn’t she? I know she might seem like overkill to some, but we get some unsavory characters in here and you can never be too careful. No need to use her, of course, just laying eyes on her dissuades anyone with….disagreeable ideas.
Detective: You own any other guns I should know about?
Dale: No, that’s all I have. Except the pistol I gave to Loretta, of course.
Detective: You gave Miss Dubois a pistol?
Dale: She was feeling a little uneasy coming into work every week. As I said, we get some unsavory characters. She told me she’d feel better knowing she could defend herself. So I did what any gentleman would do in that situation, I gave her my pistol.

<<set $DaleHasSpokenAboutShotgun to true>>

<<jump Questions>>
===
title: Drugs
position: 141,457
---
Detective: Mr. Butterbur, I have found a helluva lot of illicit substances in your office. Anything you want to say about that?
Dale: You opened my safe? Surely that’s an illegal search, Detective?
Detective: You want to talk about illegal? Then let’s talk about drug possession.
Dale: Hey, look - I know it doesn’t look great. But it doesn’t have anything to do with Jack’s death, right? You’ve got to admit I’ve been nothing but helpful with this whole investigation. And I’ll continue to be helpful. Surely you can look away just this once?
Detective: It doesn’t work like that, Butterbur. As soon as this murder is solved, we’ll have to talk further on this.

<<set $DaleHasSpokenAboutDrugs to true>>

<<jump Questions>>
===
title: PeggyVisitsOften
position: 141,457
---
Detective: I’ve been hearing some interesting things about you and Mrs. Harlow.
Dale: What? What things?
Detective: That maybe you were keeping her husband around just to see a little more of her.
Dale: Gee, people around here like to gossip, huh? Sure, Mrs. Harlow seems a wonderful woman and honestly when you serve men all day every day it’s nice to have a woman around. But there’s nothing more to it than that, Detective, I promise you.

<<set $DaleHasSpokenAboutPeggy to true>>

<<jump Questions>>
===
title: PhotoOfPeggy
position: 141,457
---
Detective: I couldn’t help but notice, Dale, that you have a photo of the victim’s wife in your office. And here I was under the impression you hardly knew her.
Dale: … 
Detective: Cat got your tongue, Mr. Butterbur? 
Dale: I can explain, Detective. It’s not what it looks like.
Detective: Really? Because it looks like you have quite an intense interest in Mrs. Harlow and you’ve been lying to me about it. Did Mr. Harlow know about your feelings towards his wife? I can’t imagine he would have been too pleased about that.
Dale: It wasn’t like that!
Detective: It must have hurt you to see her husband in here most nights, canoodling with Miss Dubois, while his wife waited faithfully for him to come home. You must have hated the way he treated her, you knew you could treat her better. But a loyal wife like Mrs. Harlow wouldn’t just leave her husband, not unless he was removed entirely…
Dale: Stop! Stop it! 
Detective: Then tell me the truth!
Dale: Alright, Detective, you win. I’ll admit….I love Peggy. And yes, it did kill me to see how that bastard treated her like she was nothing. Peggy’s no idiot, she knew what was happening with Loretta. And she still came to pick him up most nights. It broke my damn heart. 
Dale: She deserves the world, and instead she got stuck with Jack Harlow. So I’m glad he’s dead, alright? I’m damn glad. But I didn’t shoot him.
<<set $DaleHasSpokenAboutPhotoOfPeggy to true>>

<<jump Questions>>
===

title: Goodbye
position: -211,486
---

<<set $DaleRandomGoodbye to random_range(0,2)>>
<<if $DaleRandomGoodbye == 0>>
Detective: Thanks for your help
Dale: Anytime!
<<elseif $DaleRandomGoodbye == 1>>
Detective: I appreciate your help.
Dale: No problems at all, Detective.
<<elseif $DaleRandomGoodbye == 2>>
Detective: That’s all for now.
Dale: Bye.
<<endif>>

<<stop>>
===